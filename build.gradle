plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.7.0'
}

group = 'hoony'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

def openApiGeneratedDir = "$projectDir/src/main/openapi-generated".toString()

sourceSets {
    main {
        java {
            srcDirs openApiGeneratedDir
            srcDirs 'src/main/java'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
    implementation 'org.openapitools:openapi-generator:7.7.0'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/api/review.yml".toString()
    configFile = "$projectDir/api/openapi-config.json".toString()
    outputDir = openApiGeneratedDir
    globalProperties.set(
            Map.of(
                    "apis", "",
                    "models", "",
                    "supportingFiles", ""
            )
    )
    schemaMappings.set(
            Map.of(
                    "date", "LocalDate",
                    "DateTime", "LocalDateTime"
            )
    )
}

tasks.register('clearApiSources', Delete) {
    delete file(openApiGeneratedDir)
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.clean.dependsOn tasks.clearApiSources
tasks.compileJava.dependsOn tasks.openApiGenerate
tasks.openApiGenerate.dependsOn tasks.clearApiSources
